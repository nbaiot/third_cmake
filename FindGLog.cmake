include(FindPackageHandleStandardArgs)

set(CMAKE_FIND_LIBRARY_SUFFIXES_SAV ${CMAKE_FIND_LIBRARY_SUFFIXES})

if (GLOG_USE_STATIC)
    set(CMAKE_FIND_LIBRARY_SUFFIXES ${CMAKE_STATIC_LIBRARY_SUFFIX})
    set(SHARED_OR_STATIC "STATIC")
else ()
    set(CMAKE_FIND_LIBRARY_SUFFIXES ${CMAKE_FIND_LIBRARY_SUFFIXES_SAV})
    set(SHARED_OR_STATIC "SHARED")
endif ()

unset(GLOG_LIBRARY CACHE)
find_library(
        GLOG_LIBRARY
        NAMES "glog"
        PATHS ${GLOG_INSTALL_PATH}
        PATH_SUFFIXES "lib" "lib64"
        NO_DEFAULT_PATH
        NO_CMAKE_FIND_ROOT_PATH
)

unset(GLOG_INCLUDE CACHE)
find_path(GLOG_INCLUDE
        NAMES "glog/logging.h"
        PATHS ${GLOG_INSTALL_PATH}
        PATH_SUFFIXES "include"
        NO_DEFAULT_PATH
        NO_CMAKE_FIND_ROOT_PATH
        )

find_package_handle_standard_args(GLog DEFAULT_MSG GLOG_LIBRARY GLOG_INCLUDE)

if (GLOG_FOUND)
    if (NOT TARGET glog)
        add_library(glog ${SHARED_OR_STATIC} IMPORTED GLOBAL)
    endif ()
    set_property(TARGET glog PROPERTY IMPORTED_LOCATION ${GLOG_LIBRARY})
    include_directories(${GLOG_INCLUDE})
    set(GLOG_INCLUDE_DIRS ${GLOG_INCLUDE})
    set(GLOG_LIBRARIES ${GLOG_LIBRARY})
endif ()

### restore
set(CMAKE_FIND_LIBRARY_SUFFIXES ${CMAKE_FIND_LIBRARY_SUFFIXES_SAV})
set(SHARED_OR_STATIC)